apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 146
        versionName '5.0'
    }
    signingConfigs {
        release {
            def Properties localProps = new Properties()
            localProps.load(new FileInputStream(file('../local.properties')))
            def Properties keyProps = new Properties()
            keyProps.load(new FileInputStream(file(localProps['keystore.props.file'])))
            storeFile file(keyProps["store"])
            keyAlias keyProps["alias"]
            storePassword keyProps["storePass"]
            keyPassword keyProps["pass"]
        }
    }
    buildTypes {
        debug.initWith(buildTypes.debug)
        debug {
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
            manifestPlaceholders = [bugsnagApiKey: "", ormLog: false]
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.release

            def Properties bugsnagProps = new Properties()
            bugsnagProps.load(new FileInputStream(file('../bugsnag.properties')))
            manifestPlaceholders = [bugsnagApiKey: bugsnagProps["api-key"], ormLog: false]
        }
    }
    productFlavors {
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions{
        preDexLibraries = !'true'.equals(System.getenv('CI'))
        dexInProcess = true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

//    sourceSets {
//        main {
//            java {
//                srcDirs = ['src/main/kotlin']
//            }
//        }
//    }
}

dependencies {
    compile "com.android.support:support-v4:$rootProject.supportVersion"
    compile "com.android.support:appcompat-v7:$rootProject.supportVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.supportVersion"
    compile "com.android.support:support-annotations:$rootProject.supportVersion"
    compile "com.android.support:design:$rootProject.supportVersion"
    compile "com.squareup.retrofit:retrofit:$rootProject.retrofitVersion"
    compile "com.squareup.retrofit:converter-moshi:$rootProject.retrofitVersion"
    compile "com.squareup.retrofit:adapter-rxjava:$rootProject.retrofitVersion"
    compile 'com.loopeer.android.thirdparty:swipe-back-layout:1.0'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile "com.jakewharton.rxbinding:rxbinding:$rootProject.rxBindingVersion"
    compile "com.jakewharton.rxbinding:rxbinding-kotlin:$rootProject.rxBindingVersion"
    compile "com.jakewharton.rxbinding:rxbinding-support-v4:$rootProject.rxBindingVersion"
    compile "com.jakewharton.rxbinding:rxbinding-design:$rootProject.rxBindingVersion"
    compile "com.trello:rxlifecycle:$rootProject.rxLifecycleVersion"
    compile "com.trello:rxlifecycle-components:$rootProject.rxLifecycleVersion"
    compile 'com.rengwuxian.materialedittext:library:2.1.4'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.github.dmytrodanylyk.circular-progress-button:library:1.1.3'
    compile('com.mikepenz:materialdrawer:4.6.1@aar') {
        transitive = true
    }
    compile 'com.melnykov:floatingactionbutton:1.3.0'
    compile "com.github.johnkil.android-robototextview:robototextview:$rootProject.robotoTextViewVersion"
    compile "com.facebook.stetho:stetho:$rootProject.stethoVersion"
    compile "com.facebook.stetho:stetho-okhttp:$rootProject.stethoVersion"
    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    compile 'com.github.Groxx:AndroidSwipeLayout:c8a42d8'
    compile "com.google.dagger:dagger:$rootProject.daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$rootProject.daggerVersion"
    compile 'com.sothree.slidinguppanel:library:3.0.0'
    compile 'com.jakewharton:disklrucache:2.0.2'
    compile 'com.jakewharton.timber:timber:4.1.0'
    compile 'com.bugsnag:bugsnag-android:3.2.6'
    compile 'com.squareup.moshi:moshi:1.0.0'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    provided 'org.glassfish:javax.annotation:10.0-b28'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.0.31-beta'
    testApt "com.google.dagger:dagger-compiler:$rootProject.daggerVersion"
}

kapt {
    generateStubs = true
}

android.applicationVariants.all { variant ->
    variant.mergeAssets.doFirst {
        File fonts = file("${buildDir}/intermediates/exploded-aar/com.github.johnkil.android-robototextview/robototextview/${rootProject.robotoTextViewVersion}/assets/fonts")
        if (fonts.exists()) {
            for (File file : fonts.listFiles()) {
                if (file.getName().contains("RobotoSlab")) {
                    println("delete " + file.getName() + " font")
                    file.delete()
                };
                if (file.getName().contains("RobotoCondensed")) {
                    println("delete " + file.getName() + " font")
                    file.delete()
                };
                if (file.getName().contains("Roboto-BlackItalic")) {
                    println("delete " + file.getName() + " font")
                    file.delete()
                };
            }
        }
    }
}
